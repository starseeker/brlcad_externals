# LIEF is a cross platform library which can parse, modify and abstract ELF, PE
# and MachO binary formats.  We use it to define a tool for rpath manipulation.

# We only need LIEF on some platforms - don't worry about trying to build it
# everywhere
if (NOT MSVC AND NOT APPLE)

  if (ENABLE_ALL AND NOT DEFINED ENABLE_LIEF)
    set(ENABLE_LIEF ON)
  endif (ENABLE_ALL AND NOT DEFINED ENABLE_LIEF)

  if (NOT ENABLE_LIEF)

    find_package(LIEF)

    if (NOT LIEF_FOUND AND NOT DEFINED ENABLE_LIEF)
      set(ENABLE_LIEF "ON" CACHE BOOL "Enable LIEF build")
    endif (NOT LIEF_FOUND AND NOT DEFINED ENABLE_LIEF)

  endif (NOT ENABLE_LIEF)

  set(ENABLE_LIEF "${ENABLE_LIEF}" CACHE BOOL "Enable LIEF build")

  if (ENABLE_LIEF)

    ExternalProject_Add(LIEF_BLD
      URL "${CMAKE_CURRENT_SOURCE_DIR}/lief"
      BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
      CMAKE_ARGS
      $<$<NOT:$<BOOL:${CMAKE_CONFIGURATION_TYPES}>>:-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}>
      -DBIN_DIR=${BIN_DIR}
      -DLIB_DIR=${LIB_DIR}
      -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
      -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
      -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
      -DCMAKE_INSTALL_PREFIX=${CMAKE_NOBUNDLE_INSTALL_PREFIX}
      LOG_CONFIGURE ${EXT_BUILD_QUIET}
      LOG_BUILD ${EXT_BUILD_QUIET}
      LOG_INSTALL ${EXT_BUILD_QUIET}
      LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
      STEP_TARGETS install
      )

    if (TARGET PATCH_BLD)
      ExternalProject_Add_StepDependencies(LIEF_BLD configure PATCH_BLD-install)
    endif (TARGET PATCH_BLD)

  endif (ENABLE_LIEF)
endif (NOT MSVC AND NOT APPLE)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

