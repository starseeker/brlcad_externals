# Unless we have ENABLE_ALL set, based the building of png on
# the system detection results
if (ENABLE_ALL AND NOT DEFINED ENABLE_ASSETIMPORT)
  set(ENABLE_ASSETIMPORT ON)
endif (ENABLE_ALL AND NOT DEFINED ENABLE_ASSETIMPORT)

if (NOT ENABLE_ASSETIMPORT)

  find_package(ASSETIMPORT)

  if (NOT ASSETIMPORT_FOUND AND NOT DEFINED ENABLE_ASSETIMPORT)
    set(ENABLE_ASSETIMPORT "ON" CACHE BOOL "Enable assetimport build")
  endif (NOT ASSETIMPORT_FOUND AND NOT DEFINED ENABLE_ASSETIMPORT)

endif (NOT ENABLE_ASSETIMPORT)
set(ENABLE_ASSETIMPORT "${ENABLE_ASSETIMPORT}" CACHE BOOL "Enable assetimport build")

if (ENABLE_ASSETIMPORT)

  if (TARGET ZLIB_BLD)
    set(ZLIB_TARGET ZLIB_BLD)
  endif (TARGET ZLIB_BLD)

  ExternalProject_Add(ASSETIMPORT_BLD
    URL "${CMAKE_CURRENT_SOURCE_DIR}/assetimport"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    PATCH_COMMAND ${PATCH_EXECUTABLE};-E;-p1;${PATCH_OPTIONS};-i;${CMAKE_CURRENT_SOURCE_DIR}/assetimport.patch
    CMAKE_ARGS
    ${BUILD_TYPE_SPECIFIER}
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}
    -DCMAKE_INSTALL_LIBDIR=${LIB_DIR}
    -DZLIB_ROOT=$<$<BOOL:${ZLIB_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DASSIMP_BUILD_ASSIMP_TOOLS=OFF
    -DASSIMP_BUILD_TESTS=OFF
    -DASSIMP_BUILD_MINIZIP=ON
    -DASSIMP_INJECT_DEBUG_POSTFIX=OFF
    -DASSIMP_INSTALL_PDB=OFF
    DEPENDS ${ASSETIMPORT_DEPS}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  if (TARGET PATCH_BLD)
    ExternalProject_Add_StepDependencies(ASSETIMPORT_BLD configure PATCH_BLD-install)
  endif (TARGET PATCH_BLD)

  if (TARGET ZLIB_BLD)
    ExternalProject_Add_StepDependencies(ASSETIMPORT_BLD configure ZLIB_BLD-install)
  endif (TARGET ZLIB_BLD)


endif (ENABLE_ASSETIMPORT)

# Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
file(MAKE_DIRECTORY ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/assetimport/LICENSE
  ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other/assetimport.txt
  COPYONLY
  )

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

