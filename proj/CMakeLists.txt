# Unless we have ENABLE_ALL set, based the building of png on
# the system detection results
if (ENABLE_ALL AND NOT DEFINED ENABLE_PROJ)
  set(ENABLE_PROJ ON)
endif (ENABLE_ALL AND NOT DEFINED ENABLE_PROJ)

if (NOT ENABLE_PROJ)

  find_package(PROJ)

  if (NOT PROJ_FOUND AND NOT DEFINED ENABLE_PROJ)
    set(ENABLE_PROJ "ON" CACHE BOOL "Enable proj build")
  endif (NOT PROJ_FOUND AND NOT DEFINED ENABLE_PROJ)

endif (NOT ENABLE_PROJ)
set(ENABLE_PROJ "${ENABLE_PROJ}" CACHE BOOL "Enable proj build")

if (ENABLE_PROJ)

  if (TARGET SQLITE3_BLD)
    set(SQLITE3_TARGET SQLITE3_BLD)
    set(SQLite3_EXECNAME ${CMAKE_BUNDLE_INSTALL_PREFIX}/${BIN_DIR}/sqlite3${CMAKE_EXECUTABLE_SUFFIX})
  endif (TARGET SQLITE3_BLD)

  ExternalProject_Add(PROJ_BLD
    SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/proj"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    CMAKE_ARGS
    $<$<NOT:$<BOOL:${CMAKE_CONFIGURATION_TYPES}>>:-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}>
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}
    -DCMAKE_INSTALL_LIBDIR=${LIB_DIR}
    -DPROJ_LIB_DIR=${CMAKE_BUNDLE_INSTALL_PREFIX}/${DATA_DIR}/proj
    -DSQLite3_ROOT=$<$<BOOL:${SQLITE3_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DEXE_SQLITE3=${SQLite3_EXECNAME}
    -DBUILD_TESTING=OFF
    -DENABLE_TIFF=OFF
    -DENABLE_CURL=OFF
    -DBUILD_PROJSYNC=OFF
    DEPENDS ${PROJ_DEPS}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  if (TARGET PATCH_BLD)
    ExternalProject_Add_StepDependencies(PROJ_BLD configure PATCH_BLD-install)
  endif (TARGET PATCH_BLD)

  if (TARGET SQLITE3_BLD)
    ExternalProject_Add_StepDependencies(PROJ_BLD configure SQLITE3_BLD-install)
  endif (TARGET SQLITE3_BLD)

endif (ENABLE_PROJ)

# Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
file(MAKE_DIRECTORY ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/proj/COPYING
  ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other/proj.txt
  COPYONLY
  )


# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

