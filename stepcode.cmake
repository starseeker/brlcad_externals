# Unless we have ENABLE_ALL set, based the building of stepcode on
# the system detection results
if (ENABLE_ALL)
  set(ENABLE_STEPCODE ON)
endif (ENABLE_ALL)

if (NOT ENABLE_STEPCODE)

  find_package(STEPCODE)

  if (NOT STEPCODE_FOUND AND NOT DEFINED ENABLE_STEPCODE)
    set(ENABLE_STEPCODE "ON" CACHE BOOL "Enable stepcode build")
  endif (NOT STEPCODE_FOUND AND NOT DEFINED ENABLE_STEPCODE)

endif (NOT ENABLE_STEPCODE)
set(ENABLE_STEPCODE "${ENABLE_STEPCODE}" CACHE BOOL "Enable stepcode build")

if (ENABLE_STEPCODE)

  set(SC_DEPS)
  if (TARGET PERPLEX_BLD)
    set(PERPLEX_TARGET PERPLEX_BLD)
  endif (TARGET PERPLEX_BLD)

  ExternalProject_Add(STEPCODE_BLD
    SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/stepcode"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    CMAKE_ARGS
    $<$<NOT:$<BOOL:${CMAKE_CONFIGURATION_TYPES}>>:-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}>
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_STATIC_LIBS=OFF
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_RPATH=${CMAKE_INSTALL_PREFIX}/${LIB_DIR}
    $<$<BOOL:${PERPLEX_TARGET}>:-DLEMON_ROOT=${CMAKE_NOINSTALL_PREFIX}>
    $<$<BOOL:${PERPLEX_TARGET}>:-DLEMON_TEMPLATE=${CMAKE_NOINSTALL_PREFIX}/${DATA_DIR}/lemon/lempar.c>
    $<$<BOOL:${PERPLEX_TARGET}>:-DPERPLEX_ROOT=${CMAKE_NOINSTALL_PREFIX}>
    $<$<BOOL:${PERPLEX_TARGET}>:-DPERPLEX_TEMPLATE=${CMAKE_NOINSTALL_PREFIX}/${DATA_DIR}/perplex/perplex_template.c>
    $<$<BOOL:${PERPLEX_TARGET}>:-DRE2C_ROOT=${CMAKE_NOINSTALL_PREFIX}>
    -DSC_BUILD_SCHEMAS=
    -DSC_ENABLE_COVERAGE=OFF
    -DSC_ENABLE_TESTING=OFF
    -DSC_IS_SUBBUILD=ON
    -DSC_PYTHON_GENERATOR=OFF
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    )

  if (TARGET PERPLEX_BLD)
    ExternalProject_Add_StepDependencies(STEPCODE_BLD configure PERPLEX_BLD-install)
  endif (TARGET PERPLEX_BLD)

  SetTargetFolder(STEPCODE_BLD "Third Party Libraries")
  SetTargetFolder(stepcode "Third Party Libraries")

endif(ENABLE_STEPCODE)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

