# Unless we have ENABLE_ALL set, based the building of ospray on
# the system detection results
if (ENABLE_ALL AND NOT DEFINED ENABLE_OSPRAY)
  set(ENABLE_OSPRAY ON)
endif (ENABLE_ALL AND NOT DEFINED ENABLE_OSPRAY)

if (NOT ENABLE_OSPRAY)

  find_package(OSPRAY)

  if (NOT OSPRAY_FOUND AND NOT DEFINED ENABLE_OSPRAY)
    set(ENABLE_OSPRAY "ON" CACHE BOOL "Enable ospray build")
  endif (NOT OSPRAY_FOUND AND NOT DEFINED ENABLE_OSPRAY)

endif (NOT ENABLE_OSPRAY)
set(ENABLE_OSPRAY "${ENABLE_OSPRAY}" CACHE BOOL "Enable ospray build")

if (ENABLE_OSPRAY)

  set(OSPRAY_DEPENDS EMBREE ISPC RKCOMMON TBB)
  TargetVars(OSPRAY_DEPENDS)

  ExternalProject_Add(OSPRAY_BLD
    URL "${CMAKE_CURRENT_SOURCE_DIR}/ospray"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    #PATCH_COMMAND ${PATCH_EXECUTABLE};-E;-p1;${PATCH_OPTIONS};-i;${CMAKE_CURRENT_SOURCE_DIR}/ospray.patch
    CMAKE_ARGS
    ${BUILD_TYPE_SPECIFIER}
    -DEMBREE_ROOT=$<$<BOOL:${EMBREE_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DISPC_ROOT=$<$<BOOL:${ISPC_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DTBB_ROOT=$<$<BOOL:${TBB_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    -DOSPRAY_ENABLE_VOLUMES=OFF
    -DOSPRAY_ENABLE_APPS_BENCHMARK=OFF
    -DOSPRAY_ENABLE_APPS_TESTING=OFF
    -DBIN_DIR=${BIN_DIR}
    -DLIB_DIR=${LIB_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  TargetInstallDeps(OSPRAY OSPRAY_DEPENDS)

  if (TARGET PATCH_BLD)
    ExternalProject_Add_StepDependencies(OSPRAY_BLD configure PATCH_BLD-install)
  endif (TARGET PATCH_BLD)

endif (ENABLE_OSPRAY)

# Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
file(MAKE_DIRECTORY ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/ospray/LICENSE.txt
  ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other/ospray.txt
  COPYONLY
  )

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

